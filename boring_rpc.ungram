/// see: https://rust-analyzer.github.io/blog/2020/10/24/introducing-ungrammar.html

Name = '#ident'

Literal = 
  StringLiteral |
  NumberLiteral |
  BooleanLiteral |
  NullLiteral |
  ArrayLiteral |
  ObjectLiteral

StringLiteral = '#string'
NumberLiteral = '#number'
BooleanLiteral = 'true' | 'false'
NullLiteral = 'null'

ArrayLiteral = '[' LiteralList? ']'

ObjectLiteral = '{' LiteralFieldList? '}'

LiteralFieldList = LiteralField (',' LiteralField)* ','?

LiteralField = key:Name ':' value:Literal

TypeDecl = 'type' Name '=' '{' FieldList? '}'

TypeExpr = 
  Name |
  TypeArray

TypeArray = TypeExpr '[' ']'

FieldList = Field (',' Field)* ','?

Field = MacroAttrs? DecoratorAttrs? field_name:Name ':' field_type:TypeExpr

MacroAttrs = MacroAttr MacroAttr*

MacroAttr = '#' Name '(' LiteralList? ')'

DecoratorAttrs = DecoratorAttr DecoratorAttr*

DecoratorAttr = '@' Name '(' LiteralList? ')'

LiteralList = Literal (',' Literal)* ','
